---
layout: post
title: "OS - 01"
description: ""
category: 
tags: [OS]
---
{% include JB/setup %}

###Hello World
<pre><code>
	ORG	0x7c00
	JMP	entry

entry:
	MOV	AX,0
	MOV	SS,AX
	MOV	SP,0x7c00
	MOV	DS,AX
	MOV	ES,AX

	MOV	SI,msg

putloop:
	MOV	AL,[SI]
	ADD	SI,1
	CMP	AL,0
	JE	fin
	MOV	AH,0x0e
	MOV	BX,15
	INT	0x10
	JMP	putloop

fin:
	HLT
	JMP	fin
msg:
	DB	0x0a,0x0a
	DB	"hello,world"
	DB	0x0a
	DB	0

	times	510-($-$$)	db 0
	DB	0x55,0xaa
</code></pre>

###说明
`ORG	0x7c00`这段代码的作用是把当前程序加载到0x7c00处，这个是告诉编译器把代码加载到0000:7c00处。

`entry`部分的代码是对寄存器进行初始化

`putloop`部分的代码是用来输出`SI`寄存器所指向的字符串，以`\0`结尾。

`fin`部分是个死循环

`msg`部分定义了要输出的字符串

`times 510-($-$$) db 0`，其中`$`表示当前指令的位置，即是0x7c00，`$$`表示程序的起始位置,`$-$$`表示当前位置到起始位置的位移，也及时所占的字节数。`times 510-($-$$) db 0`表示从当前位置到第510字节都用0来填充。

`DB 0x55,0xaa`，最后两个字节用0x55和0xaa，用于标识该扇区是启动扇区。

###基本流程

* 计算机电源打开时，完成加点自检后，读取软盘内容，如果第一扇区(0柱面0磁道的1扇区）是以0XAA55结尾，则表示这个扇区是引导扇区。

* 发现引导扇区后，计算机会把这512字节的内容加载到内存的0000:7c00处，然后从该处执行代码。

* 然后就显示我们的`Hello World`字符串。

###编译和运行
####编译
<pre><code>nasm myOS.nas -o myOS.bin</code></pre>

####转换成img文件
<pre><code>dd if=myOS.bin of=myOS.img bs=512 count=1 conv=notrunc</code></pre>

####执行
用VirtualBox或者Qume模拟器来加载myOS.img（推荐用Qume)

###Makefile
可以使用Makefile文件来一次性完成上面的动作
<pre><code>myOS.bin : myOS.nas Makefile
	nasm myOS.nas -o myOS.bin<br/>
myOS.img : myOS.bin Makefile<br/>
	dd if=myOS.bin of=myOS.img bs=512 count=1 conv=notrunc
img:
	make -r myOS.img</code></pre>

###效果图
![](http://kenvifire.github.com/images/OS-1.jpg)

